options {
	JAVA_UNICODE_ESCAPE=true;
	STATIC=false;					// 1 seul parseur actif a la fois (+ rapide)
	MULTI=true;						// type noeud = f(nom noeud)
	VISITOR=true;					// Cree une methode accept(visiteur)
	NODE_USES_PARSER=true;
}

//
// PARSER DEFINITION
//

PARSER_BEGIN(Parser)

class Parser
{
	public static void main(String args[])
	{
		Parser c = new Parser(new java.io.DataInputStream(System.in));
		try {
			ASTProgram n = c.Program();
			ParserVisitor v = new CodeGenVisitor();
			n.jjtAccept(v,null);
			//n.dump(" ");
		} catch (Exception e) {
			System.out.println("Erreur: " + '\n' + e);
			System.exit(-1);
		}
	}
}

PARSER_END(Parser)

//
// LEXICAL ANALYSIS
//

// White space

SKIP :
{
	< ( " " | "\t" | "\n" | "\r" )+	>
}

// Comments

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}




// Keywords
TOKEN :
{
	< IF: "if"	> |
	< ELSE: "else" > |
	< WHILE: "while" > | 
	< TYPE: < STRING > | < INT > | < FLOAT > | < BOOL > > |
	< #STRING: "string"> |
	< #INT: "int"> |
	< #FLOAT: "float" > |
	< #BOOL: "bool"> |
	< PRINT: "print" > |
	< INPUT: "input" >
	// A COMPLETER
}

// Operators
TOKEN :
{
	<ASSIGN: < EQUAL > | < PLUSEQUAL > | < MINUSEQUAL > | < POWEQUAL > | < DIVEQUAL > | < FOISEQUAL > > |
	<COMPARE: < LESS > | < LESSEQUAL > | < GREAT > | <GREATEQUAL > | < DIFF > | < EQUALEQUAL > > |
	
	< #LESS: "<" > | 
	< #LESSEQUAL: "<=" > | 
	< #GREAT: ">" > | 
	< #GREATEQUAL: ">=" > | 
	< #DIFF: "!=" > | 
	< #EQUALEQUAL: "==" > |
	
	< PLUS: "+" > |
	< MINUS: "-" > |
	< FOIS: "*" > |
	< DIV: "/" > |
	< POW: "^" > |
	< AND: "&&" > |
	< OR: "||" > |
	< NOT: "not" > |
	
	< #EQUAL: "=" > |
	< #PLUSEQUAL: "+=" > |
	< #MINUSEQUAL: "-=" > |
	< #POWEQUAL: "^=" > |
	< #DIVEQUAL: "/=" > |
	< #FOISEQUAL: "*=" > |
	< LPAREN: "(" > |
	< RPAREN: ")" > |
	< LACC: "{" > |
	< RACC: "}" > |
	< COLON: ":" > |
	< SEMICOLON: ";" > |
	< COMMA: "," >
	// A COMPLETER
}


// Identifiers and numbers
TOKEN :
{
	< IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > |
	< #LETTER: ["a"-"z","A"-"Z","_"] > |
	< #DIGIT: ["0"-"9"] > |
	< INTEGER: ["1"-"9"] (["0"-"9"])* | "0" > |
	// // Caution! Allows numbers such as "0000.23"
	< REAL: (<INTEGER>)+ "." (["0"-"9"])+ (<EXPONENT>)? |
			  (<INTEGER>)+ <EXPONENT> > |
	< #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
}







//
// SYNTAX ANALYSIS
//

ASTProgram Program() : { }
{
	Block() <EOF> { return jjtThis;  }
}

void Block() : { }
{
	 ( Stmt() )*
}

void Stmt() : { }
{
	
	AssignStmt() ";" |
	WhileStmt() |
	IfStmt() 
	
	
}

void AssignStmt() : { Token assignOp; }
{
	(LOOKAHEAD(2) Identifier() assignOp = <ASSIGN>
	{
		jjtThis.addOp(assignOp.image);
	}
	)+ CompExpr()
}

void CompExpr() : { Token compareOp; }
{
	AndOrExpr() ( 
	compareOp = <COMPARE> 
	{
		jjtThis.addOp(compareOp.image);
	}
	AndOrExpr() )*
}

void AndOrExpr() : { Token operator; }
{
	NotExpr() ( (operator = "&&" | operator= "||" ) 
	{
		jjtThis.addOp(operator.image);
	}
	 NotExpr() )*
}

void NotExpr() : { int compteur = 0; }
{
	("not" { compteur++; })* 
	{
		if(compteur%2 == 1)
		{
			jjtThis.addOp("not");
		}
		else
		{
			jjtThis.addOp("");
		}
	}
	 AddExpr()
}

void AddExpr() : { Token operator; }
{
	MultExpr() ( (operator = "+"| operator = "-") 
	{
		jjtThis.addOp(operator.image);
	}
	 MultExpr() )*
}

void MultExpr() : { Token operator; }
{
	PowExpr() ( (operator = "*" | operator = "/") 
	{
		jjtThis.addOp(operator.image);
	}
	 PowExpr() )*
}

void PowExpr() : { Token operator; }
{
	NegExpr() ( operator = "^" 
	{
		jjtThis.addOp(operator.image);
	}
	NegExpr() )*
}

void NegExpr() : { int compteur = 0; }
{
	("-" { compteur++; })* 
	{
		if(compteur%2 == 1)
		{
			jjtThis.addOp("-");
		}
		else
		{
			jjtThis.addOp("");
		}
	}
	 BasicExpr() 
}

void BasicExpr() : {}
{
	Identifier()|
	IntValue() | 
	RealValue() | 
	"(" CompExpr() ")"
}

void Identifier() : { Token t; }
{
	t = <IDENTIFIER> {jjtThis.setValue(t.image);}
}

void IntValue() : { Token t; }
{
	t = <INTEGER> {jjtThis.setValue(Integer.parseInt(t.image));}
}


void RealValue() : { Token t; }
{
	t = <REAL> {jjtThis.setValue(Double.parseDouble(t.image));}
}

void WhileStmt() : {}
{
	"while" CompExpr() ":" "{" Block() "}"
}

void IfStmt() : {}
{
	"if" CompExpr() ":" "{" Block() "}" ["else" "{" Block() "}"]
}